import de.itemis.mps.gradle.BuildLanguages
import de.itemis.mps.gradle.RunAntScript


ext.mpsMajor = "2020.2"
ext.mpsMinor = "2"
ext.jbrSdkVersion = '11_0_8'
ext.jbrBuild = 'b1129.2'

group 'ir.amv.snippets'
version '1.0-SNAPSHOT'

buildscript {
    repositories {
        maven { url 'https://projects.itemis.de/nexus/content/repositories/mbeddr' }
        mavenCentral()
    }
    dependencies {
        classpath "de.itemis.mps:mps-gradle-plugin:1.2+"
        classpath 'de.undercouch:gradle-download-task:4.0.4'
    }
}

apply plugin: 'de.undercouch.download'

repositories {
    maven { url 'https://projects.itemis.de/nexus/content/repositories/mbeddr' }
    ivy {
        url "https://download.jetbrains.com/mps/$mpsMajor/"
        layout 'pattern', {
            artifact "[module]-[revision].[ext]"
        }
        metadataSources { // skip downloading ivy.xml
            artifact()
        }
    }
}

allprojects {
    repositories {
        mavenCentral()
    }
}

configurations {
    mps
    ant_lib
}

dependencies {
    mps "com.jetbrains:MPS:$mpsMajor.$mpsMinor@zip"
    ant_lib "org.apache.ant:ant-junit:1.10.1"
}

task downloadMacJBRZip(type: Download) {
    src "https://jetbrains.bintray.com/intellij-jbr/jbrsdk-$jbrSdkVersion-osx-x64-${jbrBuild}.tar.gz"
    dest file('lib/jbr.mac.tar.gz')
    overwrite false
}

task downloadLnxJBRZip(type: Download) {
    src "https://jetbrains.bintray.com/intellij-jbr/jbrsdk-${jbrSdkVersion}-linux-x64-${jbrBuild}.tar.gz"
    dest file('lib/jbr.lnx.tar.gz')
    overwrite false
}

task downloadWinJBRZip(type: Download) {
    src "https://jetbrains.bintray.com/intellij-jbr/jbrsdk-${jbrSdkVersion}-windows-x64-${jbrBuild}.tar.gz"
    dest file('lib/jbr.win.tar.gz')
    overwrite false
}

task downloadMacJBR(type: Copy, dependsOn: downloadMacJBRZip) {
    from tarTree('lib/jbr.mac.tar.gz')
    into file('lib/jbr-mac')
}

task downloadLnxJBR(type: Copy, dependsOn: downloadLnxJBRZip) {
    from tarTree('lib/jbr.lnx.tar.gz')
    into file('lib/jbr-lnx')
}

task downloadWinJBR(type: Copy, dependsOn: downloadWinJBRZip) {
    from tarTree('lib/jbr.win.tar.gz')
    into file('lib/jbr-win')
}

task downloadDependencies(type: Copy) {
    dependsOn configurations.mps

    configurations.mps.files.each {
        from zipTree(it)
    }
    into 'lib'
}

ext["itemis.mps.gradle.ant.defaultScriptClasspath"] = configurations.ant_lib.fileCollection { true }
ext["itemis.mps.gradle.ant.defaultScriptArgs"] = ["-Dbasedir=."]

task prepare(dependsOn: [downloadDependencies])

task buildBootstrap(type: BuildLanguages, dependsOn: prepare) {
    script 'build-bootstrap.xml'
}

task generate(type: RunAntScript, dependsOn: buildBootstrap) {
    script 'build/build-plugin.xml'
    targets 'generate'
}

task buildPlugin(type: BuildLanguages, dependsOn: buildBootstrap) {
    script 'build/build-plugin.xml'
}

task $buildRcp(type: BuildLanguages, dependsOn: buildPlugin) {
    script 'build/build-rcp.xml'
}

task copyStartupScripts(type: Copy, dependsOn: buildBootstrap) {
    from 'code/buildscripts/solutions/ir.amv.snippets.cats.ide.build/source_gen/ir/amv/snippets/cats/ide/build'

    include '*.bat'
    include '*.sh'
    include '*.vmoptions'
    include 'Info.plist.xml'

    into 'build/startup'
}

task buildRcp(type: RunAntScript, dependsOn: [$buildRcp, copyStartupScripts]) {
    script 'build/build-rcpdistrib.xml'
    targets 'clean', 'build'
}

task buildRcpWithJBR(type: RunAntScript, dependsOn: [$buildRcp, copyStartupScripts, downloadMacJBR, downloadLnxJBR, downloadWinJBR]) {
    script 'build/build-rcpdistrib-jbr.xml'
    targets 'clean', 'build'
}

task clean(type: Delete) {
    delete 'build'
    delete 'lib'
    delete fileTree('.').matching {
        include '**/source_gen/**'
        include '**/source_gen.caches/**'
        include '**/classes_gen/**'
    }
}
